plugins {
    id 'java-gradle-plugin'
    id 'groovy'
	// https://plugins.gradle.org/plugin/com.gradle.plugin-publish
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'de.clashsoft'
version = 'git describe --tags'.execute([], rootDir).text[1..-2] // strip v and \n
description = 'Integrate Angular frontends into your Gradle build.'

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
}

dependencies {
    testImplementation 'junit:junit:4.12'
}

gradlePlugin {
    website = 'https://github.com/Clashsoft/Angular-Gradle'
    vcsUrl = 'https://github.com/Clashsoft/Angular-Gradle'

    plugins {
        create("angularGradle") {
            id = 'de.clashsoft.angular-gradle'
            implementationClass = 'de.clashsoft.gradle.angular.AngularGradlePlugin'
            displayName = 'Angular Gradle'
            description = project.description
            tags = [ 'angular', 'frontend', 'node' ]
        }
    }
}

sourceSets {
    functionalTest {
    }
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

tasks.register('functionalTest', Test) {
	it.testLogging.showStandardStreams = true
    it.testClassesDirs = sourceSets.functionalTest.output.classesDirs
    it.classpath = sourceSets.functionalTest.runtimeClasspath
}

check.dependsOn 'functionalTest'
